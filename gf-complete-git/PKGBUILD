# Maintainer: Narthorn <narthorn@gmail.com>
# Contributor: Mingkai Dong <mingkaidong@gmail.com>

_pkgname=gf-complete
pkgname=$_pkgname-git
pkgver=r178.363da20
pkgrel=1
pkgdesc='A library implements every Galois Field multiplication technique applicable to erasure coding for storage'
arch=('i686' 'x86_64')
url='http://lab.jerasure.org/jerasure/gf-complete'
license=('custom')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=('git+http://lab.jerasure.org/jerasure/gf-complete.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/$_pkgname"
	./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$_pkgname"
	make DESTDIR="$pkgdir" install
	install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
